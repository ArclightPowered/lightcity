plugins {
    id 'java'
    id 'eclipse'
    id "org.jetbrains.gradle.plugin.idea-ext" version "1.0.1"
}

group = 'io.izzel.arclight'
version = '1.0.0'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    ivy {
        name = 'paper-velocity'
        url = 'https://api.papermc.io/v2/projects/'
        patternLayout {
            artifact "[module]/versions/[revision]/builds/[classifier]/downloads/[module]-[revision]-[classifier].[ext]"
        }
        metadataSources { artifact() }
    }
}

dependencies {
    compileOnly "com.velocitypowered:velocity-api:3.2.0-SNAPSHOT"
    compileOnly "com.velocitypowered:velocity:3.3.0-SNAPSHOT:306@jar"
    annotationProcessor "com.velocitypowered:velocity-api:3.2.0-SNAPSHOT"
}

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

def templateSource = file('src/main/templates')
def templateDest = layout.buildDirectory.dir('generated/sources/templates')
def generateTemplates = tasks.register('generateTemplates', Copy) { task ->
    def props = [
            'version': project.version
    ]
    task.inputs.properties props

    task.from templateSource
    task.into templateDest
    task.expand props
}

sourceSets.main.java.srcDir(generateTemplates.map { it.outputs })

rootProject.idea.project.settings.taskTriggers.afterSync generateTemplates
project.eclipse.synchronizationTasks(generateTemplates)
